# Prologue

Avant de présenter les textes réglementaires qui régissent le calcul des
allocations familiales, il est nécessaire de définir les structures de données
informatiques qui modélisent la situation dont parlent ces textes législatifs.

## Types de données manipulées par le programme

```catala-metadata
déclaration énumération PriseEnCharge :
  -- GardeAlternéePartageAllocations
  -- GardeAlternéeAllocataireUnique
  -- EffectiveEtPermanente
  -- ServicesSociauxAllocationVerséeÀLaFamille
  -- ServicesSociauxAllocationVerséeAuxServicesSociaux

déclaration énumération SituationObligationScolaire:
  -- Avant
  -- Pendant
  -- Après

déclaration structure Enfant :
  donnée identifiant contenu entier
  donnée obligation_scolaire contenu SituationObligationScolaire
  donnée rémuneration_mensuelle contenu argent
  donnée date_de_naissance contenu date
  donnée âge contenu entier
  donnée prise_en_charge contenu PriseEnCharge
  donnée a_déjà_ouvert_droit_aux_allocations_familiales contenu booléen
  donnée bénéficie_titre_personnel_aide_personnelle_logement contenu booléen

déclaration énumération PriseEnCompte:
  -- Complète
  -- Partagée
  -- Zéro

déclaration énumération VersementAllocations:
  -- Normal
  -- AllocationVerséeAuxServicesSociaux

déclaration énumération ÉlémentPrestationsFamiliales:
  -- PrestationAccueilJeuneEnfant
  -- AllocationsFamiliales
  -- ComplémentFamilial
  -- AllocationLogement
  -- AllocationÉducationEnfantHandicapé
  -- AllocationSoutienFamilial
  -- AllocationRentréeScolaire
  -- AllocationJournalièrePresenceParentale
```

## Champs d'applications

### Prestations familiales

```catala-metadata
déclaration champ d'application PrestationsFamiliales:
  sortie droit_ouvert condition dépend de Enfant
  sortie conditions_hors_âge condition dépend de Enfant
  interne plafond_l512_3_2 contenu argent
  sortie âge_l512_3_2 contenu entier
  sortie régime_outre_mer_l751_1 condition
  entrée date_courante contenu date
  entrée prestation_courante contenu ÉlémentPrestationsFamiliales
  entrée résidence contenu Collectivité
  smic champ d'application Smic

champ d'application PrestationsFamiliales:
  définition smic.résidence égal à résidence
  définition smic.date_courante égal à date_courante
```

### Allocations familiales

```catala-metadata
déclaration champ d'application AllocationFamilialesAvril2008:
  sortie âge_minimum_alinéa_1_l521_3 contenu entier

déclaration champ d'application EnfantLePlusÂgé:
  entrée enfants contenu collection Enfant
  sortie le_plus_âgé contenu Enfant

déclaration champ d'application AllocationsFamiliales:

  # Variables concernant le ménage
  entrée personne_charge_effective_permanente_est_parent condition
  entrée personne_charge_effective_permanente_remplit_titre_I condition
  entrée ressources_ménage contenu argent
  entrée résidence contenu Collectivité

  # Date à laquelle le calcul est effectué
  entrée date_courante contenu date

  # Variables concernant les enfants du ménage
  entrée enfants_à_charge contenu collection Enfant
  interne enfants_à_charge_droit_ouvert_prestation_familiale
    contenu collection Enfant
  interne prise_en_compte contenu PriseEnCompte dépend de Enfant
  interne versement contenu VersementAllocations dépend de Enfant

  sortie montant_versé contenu argent

  # Variables liées à la base des allications familiales
  interne droit_ouvert_base condition
  interne montant_initial_base contenu argent
  interne montant_initial_base_premier_enfant contenu argent
  interne montant_initial_base_deuxième_enfant contenu argent
  interne montant_initial_base_troisième_enfant_et_plus contenu argent
  interne rapport_enfants_total_moyen contenu décimal
  interne nombre_moyen_enfants contenu décimal
  interne nombre_total_enfants contenu décimal
  interne montant_avec_garde_alternée_base contenu argent
  interne montant_versé_base contenu argent

  # Variables liées à Mayotte
  entrée avait_enfant_à_charge_avant_1er_janvier_2012 condition
  interne montant_initial_base_troisième_enfant_mayotte contenu argent
  interne montant_initial_base_quatrième_enfant_et_plus_mayotte contenu argent

  # Variables liées à l'allocation forfaitaire
  interne droit_ouvert_forfaitaire condition dépend de Enfant
  interne montant_versé_forfaitaire_par_enfant contenu argent
  interne montant_versé_forfaitaire contenu argent

  # Variables liées aux majorations des allocations familiales
  interne droit_ouvert_majoration condition dépend de Enfant
  interne montant_initial_métropole_majoration contenu argent dépend de Enfant
  interne montant_initial_majoration contenu argent dépend de Enfant
  interne montant_avec_garde_alternée_majoration contenu argent dépend de Enfant
  interne montant_versé_majoration contenu argent

  # Variables liées au complément dégressif
  interne droit_ouvert_complément condition
  interne montant_base_complément_pour_base_et_majoration contenu argent
  interne complément_dégressif contenu argent dépend de argent
  interne montant_versé_complément_pour_base_et_majoration contenu argent
  interne montant_versé_complément_pour_forfaitaire contenu argent

  # Sous-champs d'applications
  prestations_familiales champ d'application PrestationsFamiliales
  version_avril_2008 champ d'application AllocationFamilialesAvril2008
  enfant_le_plus_âgé champ d'application EnfantLePlusÂgé
  bmaf champ d'application BaseMensuelleAllocationsFamiliales

  # Plafonds, âges limites et autres constantes
  interne nombre_enfants_l521_1 contenu entier
  interne âge_minimum_alinéa_1_l521_3 contenu entier dépend de Enfant
  interne nombre_enfants_alinéa_2_l521_3 contenu entier
  interne est_enfant_le_plus_âgé contenu booléen dépend de Enfant
  interne plafond_I_d521_3 contenu argent
  interne plafond_II_d521_3 contenu argent

champ d'application AllocationsFamiliales:
  définition prestations_familiales.prestation_courante égal à
    AllocationsFamiliales
  définition prestations_familiales.date_courante égal à
    date_courante
  définition prestations_familiales.résidence égal à
    résidence
  définition bmaf.date_courante égal à date_courante
```
# Montant des plafonds de ressources

## Circulaire interministérielle N° DSS/SD2B/2017/352 du 22 décembre 2017 relative à la revalorisation au 1er janvier 2018 des plafonds de ressources d’attribution de certaines prestations familiales servies en métropole, en Guadeloupe, en Guyane, en Martinique, à la Réunion, à Saint-Barthélemy, à Saint-Martin et à Mayotte

Plafonds de ressources applicables pour l’attribution du montant modulé
des allocations familiales, de la majoration pour âge et de l’allocation
forfaitaire, du 1er janvier 2018 au 31 décembre 2018 (à comparer au revenu
 net catégoriel de l'année 2016).

La première tranche est celle dont les revenus sont inférieurs ou égaux
à un plafond de base de 56 286 € majoré de 5 628 euros par enfant
à charge. La deuxième tranche est celle dont les revenus sont supérieurs à un
plafond de base de 56 286 euros majoré de 5 628 euros par enfant à charge mais
inférieurs ou égaux au plafond de base de 78 770 euros majoré de 5 628 euros
par enfant à charge. La troisième tranche est celle dont les revenus
sont supérieurs au plafond de base de 78 770 euros majoré de
5 628 euros par enfant à charge.

```catala
champ d'application AllocationsFamiliales :
  exception
  définition plafond_I_d521_3 sous condition
    date_courante >=@ |2018-01-01| et date_courante <=@ |2018-12-31|
  conséquence égal à 56 286 € +€
    5 628 € *€ (entier_vers_décimal de
      (nombre de enfants_à_charge_droit_ouvert_prestation_familiale))

  exception
  définition plafond_II_d521_3 sous condition
    date_courante >=@ |2018-01-01| et date_courante <=@ |2018-12-31|
  conséquence égal à 78 770 € +€
    5 628 € *€ (entier_vers_décimal de
      (nombre de enfants_à_charge_droit_ouvert_prestation_familiale))
```

## Instruction interministérielle n° DSS/SD2B/2018/279 du 17 décembre 2018 relative à la revalorisation au 1er janvier 2019 des plafonds de ressources d’attribution de certaines prestations familiales servies en métropole, en Guadeloupe, en Guyane, en Martinique, à la Réunion, à Saint-Barthélemy, à Saint-Martin et à Mayotte

Plafonds de ressources applicables pour l’attribution du montant
modulé des allocations familiales, de la majoration pour âge et
de l’allocation forfaitaire, du 1er janvier 2019 au 31 décembre
2019 (à comparer au revenu net catégoriel de l'année 2017).

La première tranche est celle dont les revenus sont inférieurs ou
égaux à un plafond de base de 56 849 € majoré de 5 684 euros
par enfant à charge. La deuxième tranche est celle dont les revenus sont
supérieurs à un plafond de base de 56 849 euros majoré de 5 684 euros
par enfant à charge mais inférieurs ou égaux au plafond de base de
79 558 euros majoré de 5 684 euros par enfant à charge. La troisième
tranche est celle dont les revenus sont supérieurs au plafond de
base de 79 558 euros majoré de 5 684 euros par enfant à charge.

```catala
champ d'application AllocationsFamiliales :
  exception
  définition plafond_I_d521_3 sous condition
    date_courante >=@ |2019-01-01| et date_courante <=@ |2019-12-31|
  conséquence égal à 56 849 € +€
    5 684 € *€ (entier_vers_décimal de
      (nombre de enfants_à_charge_droit_ouvert_prestation_familiale))

  exception
  définition plafond_II_d521_3 sous condition
    date_courante >=@ |2019-01-01| et date_courante <=@ |2019-12-31|
  conséquence égal à 79 558 € +€
    5 684 € *€ (entier_vers_décimal de
      (nombre de enfants_à_charge_droit_ouvert_prestation_familiale))
```

## Instruction interministerielle no DSS/SD2B/2019/261 du 18 décembre 2019 relative à la revalorisation au 1er janvier 2020 des plafonds de ressources d’attribution de certaines prestations familiales servies en métropole, en Guadeloupe, en Guyane, en Martinique, à La Réunion, à Saint-Barthélemy, à Saint-Martin et à Mayotte

Plafonds de ressources applicables pour l’attribution du montant modulé des
allocations familiales, de la majoration pour âge et de l’allocation forfaitaire,
du 1er janvier 2020 au 31 décembre 2020 (à comparer au revenu net catégoriel de
l’année 2018)

La première tranche est celle dont les revenus sont inférieurs ou
égaux à un plafond de base de 57 759 € majoré de 5 775 € par
enfant à charge. La deuxième tranche est celle dont les revenus
sont supérieurs à un plafond de base de 57 759 € majoré de 5 775
€ par enfant à charge mais inférieurs ou égaux au plafond de base
de 80 831 € majoré de 5 775 € par enfant à charge. La troisième
tranche est celle dont les revenus sont supérieurs au plafond de base de
80 831 € majoré de 5 775 € par enfant à charge.

```catala
champ d'application AllocationsFamiliales :
  exception
  définition plafond_I_d521_3 sous condition
    date_courante >=@ |2020-01-01| et date_courante <=@ |2020-12-31|
  conséquence égal à 57 759 € +€
    5 775 € *€ (entier_vers_décimal de
      (nombre de enfants_à_charge_droit_ouvert_prestation_familiale))

  exception
  définition plafond_II_d521_3 sous condition
    date_courante >=@ |2020-01-01| et date_courante <=@ |2020-12-31|
  conséquence égal à 80 831 € +€
    5 775 € *€ (entier_vers_décimal de
      (nombre de enfants_à_charge_droit_ouvert_prestation_familiale))
```

## Arrêté du 14 décembre 2020 relatif au montant des plafonds de ressources de certaines prestations familiales et aux tranches du barème applicable au recouvrement des indus et à la saisie des prestations

### Article 1|JORFARTI000042694985

I. - Le plafond de ressources prévu au 1° du I des articles D. 521-1 et D. 521-2
du code de la sécurité sociale relatif aux allocations familiales, à la majoration
pour âge et à l'allocation forfaitaire prévue au deuxième alinéa de l'article
L. 521-1 est fixé à 58 279 euros pour la période du 1er janvier 2021 au 31
décembre 2021. Il est majoré de 5 827 euros par enfant à charge.

```catala
champ d'application AllocationsFamiliales :
  exception
  définition plafond_I_d521_3 sous condition
    date_courante >=@ |2021-01-01| et date_courante <=@ |2021-12-31|
  conséquence égal à 58 279 € +€
    5 827 € *€ (entier_vers_décimal de
      (nombre de enfants_à_charge_droit_ouvert_prestation_familiale))
```

II. - Le plafond de ressources prévu au 2° du I des articles D. 521-1 et
D. 521-2 du même code relatif aux allocations familiales, à la majoration
pour âge et à l'allocation forfaitaire prévue au deuxième alinéa de l'article
L. 521-1 est fixé à 81 558 euros pour la période du 1er janvier 2021 au 31
décembre 2021. Il est majoré de 5 827 euros par enfant à charge.

```catala
champ d'application AllocationsFamiliales :
  exception
  définition plafond_II_d521_3 sous condition
    date_courante >=@ |2021-01-01| et date_courante <=@ |2021-12-31|
  conséquence égal à 81 558 € +€
    5 827 € *€ (entier_vers_décimal de
      (nombre de enfants_à_charge_droit_ouvert_prestation_familiale))
```

# Dispositions spéciales relatives à Mayotte

## Décret n°2002-423 du 29 mars 2002 relatif aux prestations familiales à Mayotte

### Article 7|LEGIARTI000036330048

Les taux servant au calcul des allocations familiales sont fixés, en
pourcentage de la base mensuelle de calcul des allocations familiales
prévue à l'article L. 551-1 du code de la sécurité sociale, ainsi qu'il suit :

```catala
champ d'application AllocationsFamiliales sous condition résidence = Mayotte:
  # La formule change à Mayotte par rapport au cas général.
  exception définition montant_initial_base égal à
    montant_initial_base_premier_enfant +€
    montant_initial_base_deuxième_enfant +€
    montant_initial_base_troisième_enfant_mayotte +€
    montant_initial_base_quatrième_enfant_et_plus_mayotte

  exception
  règle droit_ouvert_base sous condition
    (nombre de enfants_à_charge_droit_ouvert_prestation_familiale >= 1)
  conséquence rempli
```

1° 5,88 % pour un seul enfant à charge ;
```catala
champ d'application AllocationsFamiliales sous condition résidence = Mayotte :
  étiquette mayotte exception base
  définition montant_initial_base_premier_enfant égal à
    si nombre de enfants_à_charge_droit_ouvert_prestation_familiale > 0
    alors bmaf.montant *€ 5,88 %
    sinon 0 €
```

2° 32 % pour deux enfants à charge ;
```catala
champ d'application AllocationsFamiliales sous condition résidence = Mayotte:
  étiquette mayotte exception base
  définition montant_initial_base_deuxième_enfant égal à
    si nombre de enfants_à_charge_droit_ouvert_prestation_familiale > 1
    alors bmaf.montant *€ 32 %
    sinon 0 €
```

3° 16 % pour le troisième enfant à charge ;
```catala
champ d'application AllocationsFamiliales :
  définition montant_initial_base_troisième_enfant_mayotte égal à
    si nombre de enfants_à_charge_droit_ouvert_prestation_familiale > 2
    alors bmaf.montant *€ 16 %
    sinon 0 €
```

4° 4,63 % pour le quatrième enfant à charge et chacun des suivants.
```catala
champ d'application AllocationsFamiliales :
  définition montant_initial_base_quatrième_enfant_et_plus_mayotte égal à
    si nombre de enfants_à_charge_droit_ouvert_prestation_familiale > 3
    alors (bmaf.montant *€ 4,63 %) *€ (
      entier_vers_décimal de
        ((nombre de enfants_à_charge_droit_ouvert_prestation_familiale) - 3)
    ) sinon 0 €
```

### Annexe|LEGIARTI000036326574

TABLEAU D'ÉVOLUTION DES TAUX SERVANT AU CALCUL DES ALLOCATIONS FAMILIALES
APPLICABLES DANS LE DÉPARTEMENT DE MAYOTTE POUR LA PÉRIODE DU 1ER JANVIER
2012 AU 1ER JANVIER 2021, FIXÉS EN APPLICATION DE L'ARTICLE 7 DE L'ORDONNANCE
N° 2002-149 DU 7 FÉVRIER 2002 MODIFIÉE SUSVISÉE


Les taux servant au calcul des allocations familiales (AF) sont fixés, dans le
tableau ci-après, au 1er janvier de chaque année, en pourcentage de la base
mensuelle de calcul des allocations familiales (BMAF) prévue à l'article L.
551-1 du code de la sécurité sociale.

-----------------------------------------------------------------------------------------------------------------------------------------------------------------------
 Année            Montant AF pour les      Taux BMAF pour les        Taux BMAF pour les familles Taux BMAF pour les familles  Taux BMAF pour le  Taux BMAF par enfant
                  familles ayant un enfant familles ayant un         ayant deux enfants à charge ayant trois enfants à charge 3e enfant à charge supplémentaire à partir
                  à charge avant le 1er    enfant à charge à compter                                                                             du 4e enfant à charge
                  janvier 2012             du 1er janvier 2012
----------------- ------------------------ ------------------------- --------------------------- ---------------------------- ------------------ -----------------------
2011              57,28 €                  14,50 %                   23,2 %                      27,83 %                      4,63 %             4,63 %

2012              57,28 €                  13,93 %                   23,79 %                     29,18 %                      5,39 %             4,63 %

2013              57,28 €                  13,35 %                   24,37 %                     30,52 %                      6,15 %             4,63 %

2014              57,28 €                  12,78 %                   24,96 %                     31,86 %                      6,90 %             4,63 %

2015              57,28 €                  12,20 %                   25,55 %                     33,21 %                      7,66 %             4,63 %

2016              57,28 €                  11,63 %                   26,13 %                     34,55 %                      8,42 %             4,63 %

2017              57,28 €                  11,05 %                   26,72 %                     35,90 %                      9,18 %             4,63 %

2018              57,28 €                  9,76 %                    28,04 %                     38,93 %                      10,89 %            4,63 %

2019              57,28 €                  8,47 %                    29,36 %                     41,95 %                      12,59 %            4,63 %

2020              57,28 €                  7,17 %                    30,68 %                     44,98 %                      14,30 %            4,63 %

A compter de 2021 57,28 €                  5,88 %                    32 %                        48 %                         16 %               4,63 %
------------------------------------------------------------------------------------------------------------------------------------------------------------------------

```catala
champ d'application AllocationsFamiliales sous condition résidence = Mayotte:

  # Premier enfant
  exception mayotte définition montant_initial_base_premier_enfant sous condition
    date_courante >=@ |2011-01-01| et date_courante <=@ |2011-12-31| et
    non avait_enfant_à_charge_avant_1er_janvier_2012
  conséquence égal à
    si nombre de enfants_à_charge_droit_ouvert_prestation_familiale > 0
    alors bmaf.montant *€ 14,50 %
    sinon 0 €

  exception mayotte définition montant_initial_base_premier_enfant sous condition
    date_courante >=@ |2012-01-01| et date_courante <=@ |2012-12-31| et
    non avait_enfant_à_charge_avant_1er_janvier_2012
  conséquence égal à
    si nombre de enfants_à_charge_droit_ouvert_prestation_familiale > 0
    alors bmaf.montant *€ 13,93 %
    sinon 0 €

  exception mayotte définition montant_initial_base_premier_enfant sous condition
    date_courante >=@ |2013-01-01| et date_courante <=@ |2013-12-31| et
    non avait_enfant_à_charge_avant_1er_janvier_2012
  conséquence égal à
    si nombre de enfants_à_charge_droit_ouvert_prestation_familiale > 0
    alors bmaf.montant *€ 13,35 %
    sinon 0 €

  exception mayotte définition montant_initial_base_premier_enfant sous condition
    date_courante >=@ |2014-01-01| et date_courante <=@ |2014-12-31| et
    non avait_enfant_à_charge_avant_1er_janvier_2012
  conséquence égal à
    si nombre de enfants_à_charge_droit_ouvert_prestation_familiale > 0
    alors bmaf.montant *€ 12,78 %
    sinon 0 €

  exception mayotte définition montant_initial_base_premier_enfant sous condition
    date_courante >=@ |2015-01-01| et date_courante <=@ |2015-12-31| et
    non avait_enfant_à_charge_avant_1er_janvier_2012
  conséquence égal à
    si nombre de enfants_à_charge_droit_ouvert_prestation_familiale > 0
    alors bmaf.montant *€ 12,20 %
    sinon 0 €

  exception mayotte définition montant_initial_base_premier_enfant sous condition
    date_courante >=@ |2016-01-01| et date_courante <=@ |2016-12-31| et
    non avait_enfant_à_charge_avant_1er_janvier_2012
  conséquence égal à
    si nombre de enfants_à_charge_droit_ouvert_prestation_familiale > 0
    alors bmaf.montant *€ 11,63 %
    sinon 0 €

  exception mayotte définition montant_initial_base_premier_enfant sous condition
    date_courante >=@ |2017-01-01| et date_courante <=@ |2017-12-31| et
    non avait_enfant_à_charge_avant_1er_janvier_2012
  conséquence égal à
    si nombre de enfants_à_charge_droit_ouvert_prestation_familiale > 0
    alors bmaf.montant *€ 11,05 %
    sinon 0 €

  exception mayotte définition montant_initial_base_premier_enfant sous condition
    date_courante >=@ |2018-01-01| et date_courante <=@ |2018-12-31| et
    non avait_enfant_à_charge_avant_1er_janvier_2012
  conséquence égal à
    si nombre de enfants_à_charge_droit_ouvert_prestation_familiale > 0
    alors bmaf.montant *€ 9,76 %
    sinon 0 €

  exception mayotte définition montant_initial_base_premier_enfant sous condition
    date_courante >=@ |2019-01-01| et date_courante <=@ |2019-12-31| et
    non avait_enfant_à_charge_avant_1er_janvier_2012
  conséquence égal à
    si nombre de enfants_à_charge_droit_ouvert_prestation_familiale > 0
    alors bmaf.montant *€ 8,47 %
    sinon 0 €

  exception mayotte définition montant_initial_base_premier_enfant sous condition
    date_courante >=@ |2020-01-01| et date_courante <=@ |2020-12-31| et
    non avait_enfant_à_charge_avant_1er_janvier_2012
  conséquence égal à
    si nombre de enfants_à_charge_droit_ouvert_prestation_familiale > 0
    alors bmaf.montant *€ 7,17%
    sinon 0 €

  exception mayotte définition montant_initial_base_premier_enfant sous condition
    avait_enfant_à_charge_avant_1er_janvier_2012
  conséquence égal à
    si nombre de enfants_à_charge_droit_ouvert_prestation_familiale > 0
    alors 57,28€
    sinon 0 €

  # Deuxième enfant
  exception mayotte définition montant_initial_base_deuxième_enfant sous condition
    date_courante >=@ |2011-01-01| et date_courante <=@ |2011-12-31|
  conséquence égal à
    si nombre de enfants_à_charge_droit_ouvert_prestation_familiale > 1
    alors bmaf.montant *€ 23,2 %
    sinon 0 €

  exception mayotte définition montant_initial_base_deuxième_enfant sous condition
    date_courante >=@ |2012-01-01| et date_courante <=@ |2012-12-31|
  conséquence égal à
    si nombre de enfants_à_charge_droit_ouvert_prestation_familiale > 1
    alors bmaf.montant *€ 23,79 %
    sinon 0 €

  exception mayotte définition montant_initial_base_deuxième_enfant sous condition
    date_courante >=@ |2013-01-01| et date_courante <=@ |2013-12-31|
  conséquence égal à
    si nombre de enfants_à_charge_droit_ouvert_prestation_familiale > 1
    alors bmaf.montant *€ 24,37 %
    sinon 0 €

  exception mayotte définition montant_initial_base_deuxième_enfant sous condition
    date_courante >=@ |2014-01-01| et date_courante <=@ |2014-12-31|
  conséquence égal à
    si nombre de enfants_à_charge_droit_ouvert_prestation_familiale > 1
    alors bmaf.montant *€ 24,96 %
    sinon 0 €

  exception mayotte définition montant_initial_base_deuxième_enfant sous condition
    date_courante >=@ |2015-01-01| et date_courante <=@ |2015-12-31|
  conséquence égal à
    si nombre de enfants_à_charge_droit_ouvert_prestation_familiale > 1
    alors bmaf.montant *€ 25,55 %
    sinon 0 €

  exception mayotte définition montant_initial_base_deuxième_enfant sous condition
    date_courante >=@ |2016-01-01| et date_courante <=@ |2016-12-31|
  conséquence égal à
    si nombre de enfants_à_charge_droit_ouvert_prestation_familiale > 1
    alors bmaf.montant *€ 26,13 %
    sinon 0 €

  exception mayotte définition montant_initial_base_deuxième_enfant sous condition
    date_courante >=@ |2017-01-01| et date_courante <=@ |2017-12-31|
  conséquence égal à
    si nombre de enfants_à_charge_droit_ouvert_prestation_familiale > 1
    alors bmaf.montant *€ 26,72 %
    sinon 0 €

  exception mayotte définition montant_initial_base_deuxième_enfant sous condition
    date_courante >=@ |2018-01-01| et date_courante <=@ |2018-12-31|
  conséquence égal à
    si nombre de enfants_à_charge_droit_ouvert_prestation_familiale > 1
    alors bmaf.montant *€ 28,04 %
    sinon 0 €

  exception mayotte définition montant_initial_base_deuxième_enfant sous condition
    date_courante >=@ |2019-01-01| et date_courante <=@ |2019-12-31|
  conséquence égal à
    si nombre de enfants_à_charge_droit_ouvert_prestation_familiale > 1
    alors bmaf.montant *€ 29,36 %
    sinon 0 €

  exception mayotte définition montant_initial_base_deuxième_enfant sous condition
    date_courante >=@ |2020-01-01| et date_courante <=@ |2020-12-31|
  conséquence égal à
    si nombre de enfants_à_charge_droit_ouvert_prestation_familiale > 1
    alors bmaf.montant *€ 30,68 %
    sinon 0 €

  # Troisième enfant
  exception définition montant_initial_base_troisième_enfant_mayotte sous condition
    date_courante >=@ |2011-01-01| et date_courante <=@ |2011-12-31|
  conséquence égal à
    si nombre de enfants_à_charge_droit_ouvert_prestation_familiale > 2
    alors bmaf.montant *€ 4,63 %
    sinon 0 €

  exception définition montant_initial_base_troisième_enfant_mayotte sous condition
    date_courante >=@ |2012-01-01| et date_courante <=@ |2012-12-31|
  conséquence égal à
    si nombre de enfants_à_charge_droit_ouvert_prestation_familiale > 2
    alors bmaf.montant *€ 5,39 %
    sinon 0 €

  exception définition montant_initial_base_troisième_enfant_mayotte sous condition
    date_courante >=@ |2013-01-01| et date_courante <=@ |2013-12-31|
  conséquence égal à
    si nombre de enfants_à_charge_droit_ouvert_prestation_familiale > 2
    alors bmaf.montant *€ 6,15 %
    sinon 0 €

  exception définition montant_initial_base_troisième_enfant_mayotte sous condition
    date_courante >=@ |2014-01-01| et date_courante <=@ |2014-12-31|
  conséquence égal à
    si nombre de enfants_à_charge_droit_ouvert_prestation_familiale > 2
    alors bmaf.montant *€ 6,90 %
    sinon 0 €

  exception définition montant_initial_base_troisième_enfant_mayotte sous condition
    date_courante >=@ |2015-01-01| et date_courante <=@ |2015-12-31|
  conséquence égal à
    si nombre de enfants_à_charge_droit_ouvert_prestation_familiale > 2
    alors bmaf.montant *€ 7,66 %
    sinon 0 €

  exception définition montant_initial_base_troisième_enfant_mayotte sous condition
    date_courante >=@ |2016-01-01| et date_courante <=@ |2016-12-31|
  conséquence égal à
    si nombre de enfants_à_charge_droit_ouvert_prestation_familiale > 2
    alors bmaf.montant *€ 8,42 %
    sinon 0 €

  exception définition montant_initial_base_troisième_enfant_mayotte sous condition
    date_courante >=@ |2017-01-01| et date_courante <=@ |2017-12-31|
  conséquence égal à
    si nombre de enfants_à_charge_droit_ouvert_prestation_familiale > 2
    alors bmaf.montant *€ 9,18 %
    sinon 0 €

  exception définition montant_initial_base_troisième_enfant_mayotte sous condition
    date_courante >=@ |2018-01-01| et date_courante <=@ |2018-12-31|
  conséquence égal à
    si nombre de enfants_à_charge_droit_ouvert_prestation_familiale > 2
    alors bmaf.montant *€ 10,89 %
    sinon 0 €

  exception définition montant_initial_base_troisième_enfant_mayotte sous condition
    date_courante >=@ |2019-01-01| et date_courante <=@ |2019-12-31|
  conséquence égal à
    si nombre de enfants_à_charge_droit_ouvert_prestation_familiale > 2
    alors bmaf.montant *€ 12,59 %
    sinon 0 €

  exception définition montant_initial_base_troisième_enfant_mayotte sous condition
    date_courante >=@ |2020-01-01| et date_courante <=@ |2020-12-31|
  conséquence égal à
    si nombre de enfants_à_charge_droit_ouvert_prestation_familiale > 2
    alors bmaf.montant *€ 14,30 %
    sinon 0 €
```
# Code de la sécurité sociale

## Partie législative

### Livre 5 : Prestations familiales et prestations assimilées

#### Titre 1 : Champ d'application - Généralités

##### Chapitre 1er : Liste des prestations

###### Article L511-1|LEGIARTI000038834530

Les prestations familiales comprennent :

1°) la prestation d'accueil du jeune enfant ;

2°) les allocations familiales ;

3°) le complément familial ;

4°) L'allocation de logement régie par les dispositions du livre VIII du code de la construction et de l'habitation ;

5°) l'allocation d'éducation de l'enfant handicapé ;

6°) l'allocation de soutien familial ;

7°) l'allocation de rentrée scolaire ;

8°) (Abrogé) ;

9°) l'allocation journalière de présence parentale.

```catala
# Voir l'énumération ÉlémentPrestationsFamiliale
```

##### Chapitre 2 : Champ d'application

###### Article L512-3|LEGIARTI000038834523

Sous réserve des règles particulières à chaque prestation,
ouvre droit aux prestations familiales :

1°) tout enfant jusqu'à la fin de l'obligation scolaire ;

```catala
champ d'application PrestationsFamiliales :
  étiquette cas_base règle droit_ouvert de enfant sous condition
    enfant.obligation_scolaire sous forme Avant ou
    enfant.obligation_scolaire sous forme Pendant
  conséquence rempli
```

2°) après la fin de l'obligation scolaire, et jusqu'à un âge limite,
tout enfant dont la rémunération éventuelle n'excède pas un plafond.

```catala
champ d'application PrestationsFamiliales :
  étiquette cas_base règle droit_ouvert de enfant sous condition
    enfant.obligation_scolaire sous forme Après et
    (enfant.rémuneration_mensuelle <=€ plafond_l512_3_2) et
    (enfant.âge < âge_l512_3_2)
  conséquence rempli

  # On définit les conditions hors âge d'abord car elles
  # sont référencées plus tard dans l'article L521-1
  règle conditions_hors_âge de enfant sous condition
    enfant.obligation_scolaire sous forme Avant ou
    enfant.obligation_scolaire sous forme Pendant ou
    enfant.obligation_scolaire sous forme Après et
    (enfant.rémuneration_mensuelle <=€ plafond_l512_3_2)
  conséquence rempli

champ d'application AllocationsFamiliales:
  définition enfants_à_charge_droit_ouvert_prestation_familiale égal à
    filtre pour enfant dans enfants_à_charge de
      prestations_familiales.droit_ouvert de enfant
```

Toutefois, pour l'attribution du complément familial et de l'allocation
de logement mentionnés aux 3° et 4° de l'article L. 511-1 , l'âge limite
peut être différent de celui mentionné au 2° du présent article.

```catala
# Sera géré avec une exception dans Catala. Le texte est équivalent
# à une méta-propriété du code à vérifier : i.e. pas d'exceptions pour
# cet âge autre part que pour Complément Familial ou AllocationLogement.
```

#### Titre 2 : Prestations générales d'entretien

##### Chapitre 1er : Allocations familiales

###### Article L521-1|LEGIARTI000029963006

Les allocations familiales sont dues à partir du deuxième enfant à charge.

```catala
champ d'application AllocationsFamiliales :
  règle droit_ouvert_base sous condition
    nombre de enfants_à_charge_droit_ouvert_prestation_familiale >= 2
  conséquence rempli
```

Une allocation forfaitaire par enfant d'un montant fixé par décret est versée
pendant un an à la personne ou au ménage qui assume la charge d'un nombre
minimum d'enfants également fixé par décret lorsque l'un ou plusieurs des
enfants qui ouvraient droit aux allocations familiales atteignent l'âge
limite mentionné au 2° de l'article L. 512-3 . Cette allocation est versée
à la condition que le ou les enfants répondent aux conditions autres que
celles de l'âge pour l'ouverture du droit aux allocations familiales.

```catala
champ d'application AllocationsFamiliales :
  assertion fixé montant_versé par décret

  règle droit_ouvert_forfaitaire de enfant sous condition
    # nombre_enfants_alinéa_2_l521_3 sera défini dans l'article R521-3
    (nombre de enfants_à_charge >= nombre_enfants_alinéa_2_l521_3) et
    # Puisqu'un enfant ne garde un âge donné que pour une période d'un an,
    # cette condition assure que l'allocation ne peut être distribuée que pour
    # un an.
    (enfant.âge = prestations_familiales.âge_l512_3_2) et
    (enfant.a_déjà_ouvert_droit_aux_allocations_familiales) et
    (prestations_familiales.conditions_hors_âge de enfant)
  conséquence rempli
```

Le montant des allocations mentionnées aux deux premiers alinéas du présent
article, ainsi que celui des majorations mentionnées à l'article L. 521-3
varient en fonction des ressources du ménage ou de la personne qui a la charge
des enfants, selon un barème défini par décret.

```catala
champ d'application AllocationsFamiliales :
  assertion fixé montant_versé_base  par décret
  assertion varie montant_versé_base avec ressources_ménage
  assertion fixé montant_versé_majorations par décret
  assertion varie montant_versé_majorations avec ressources_ménage
```

Le montant des allocations familiales varie en fonction du nombre d'enfants
à charge.

```catala
champ d'application AllocationsFamiliales :
  assertion varie montant_versé_base.montant_versé avec
     nombre de enfants_à_charge_droit_ouvert_prestation_familiale
```

Les niveaux des plafonds de ressources, qui varient en fonction du nombre
d'enfants à charge, sont révisés conformément à l'évolution annuelle de
l'indice des prix à la consommation, hors tabac.

```catala
# Pour formaliser l'évolution des prix, il faudrait recopier ici
# tous les décrets d'application qui fixent la valeur des plafonds
# de ressources. Si cela reste possible à faire pour notre langage,
# nous avons choisi de ne pas inclure tout ce code dans ce document.
```

Un complément dégressif est versé lorsque les ressources du bénéficiaire
dépassent l'un des plafonds, dans la limite de montants définis par décret.
Les modalités de calcul de ces montants et celles du complément dégressif
sont définies par décret.

```catala
# Ditto, le volume du code à inclure pour formaliser cette assertion
# est assez important et nous avons choisi de ne pas l'inclure dans ce
# document.

champ d'application AllocationsFamiliales :
  assertion fixé montant_versé_complément par décret
```

###### Article L521-2|LEGIARTI000006743210

Les allocations sont versées à la personne qui assume, dans quelques conditions
que ce soit, la charge effective et permanente de l'enfant.

```catala
champ d'application AllocationsFamiliales :
  définition prise_en_compte de enfant sous condition
    enfant.prise_en_charge sous forme EffectiveEtPermanente
  conséquence égal à PriseEnCompte.Complète

  définition versement de enfant sous condition
    enfant.prise_en_charge sous forme EffectiveEtPermanente
  conséquence égal à VersementAllocations.Normal
```

En cas de résidence alternée de l'enfant au domicile de chacun des parents telle
que prévue à l'article 373-2-9 du code civil, mise en oeuvre de manière
effective, les parents désignent l'allocataire. Cependant, la charge de l'enfant
pour le calcul des allocations familiales est partagée par moitié entre les deux
parents soit sur demande conjointe des parents, soit si les parents sont en
désaccord sur la désignation de l'allocataire. Un décret en Conseil d'Etat fixe
les conditions d'application du présent alinéa.

```catala
champ d'application AllocationsFamiliales :
  # Premier cas : garde alternée, parents désignent un unique allocataire
  définition prise_en_compte de enfant sous condition
    enfant.prise_en_charge sous forme GardeAlternéeAllocataireUnique
  conséquence égal à PriseEnCompte.Complète

  définition versement de enfant sous condition
    enfant.prise_en_charge sous forme GardeAlternéeAllocataireUnique
  conséquence égal à VersementAllocations.Normal

  # Deuxième cas : garde alternée, parents partagent la charge pour
  # l'allocation
  définition prise_en_compte de enfant sous condition
    enfant.prise_en_charge sous forme GardeAlternéePartageAllocations
  conséquence égal à PriseEnCompte.Partagée

  définition versement de enfant sous condition
    enfant.prise_en_charge sous forme GardeAlternéePartageAllocations
  conséquence égal à VersementAllocations.Normal
```

Lorsque la personne qui assume la charge effective et permanente de l'enfant ne
remplit pas les conditions prévues au titre I du présent livre pour l'ouverture
du droit aux allocations familiales, ce droit s'ouvre du chef du père ou,
à défaut, du chef de la mère.

```catala
champ d'application AllocationsFamiliales :
  assertion
    personne_charge_effective_permanente_est_parent ou
    (
      (non personne_charge_effective_permanente_est_parent) et
      personne_charge_effective_permanente_remplit_titre_I
    )
```

Lorsqu'un enfant est confié au service d'aide sociale à l'enfance, les
allocations familiales continuent d'être évaluées en tenant compte à la fois
des enfants présents au foyer et du ou des enfants confiés au service de
l'aide sociale à l'enfance. La part des allocations familiales dues à la
famille pour cet enfant est versée à ce service. Toutefois, le juge peut
décider, d'office ou sur saisine du président du conseil général, à la
suite d'une mesure prise en application des articles 375-3 et 375-5 du code
civil ou des articles 15,16,16 bis et 28 de l' ordonnance n° 45-174 du 2
février 1945 relative à l'enfance délinquante, de maintenir le versement
des allocations à la famille, lorsque celle-ci participe à la prise en
charge morale ou matérielle de l'enfant ou en vue de faciliter le retour
de l'enfant dans son foyer.

```catala
champ d'application AllocationsFamiliales :
 définition prise_en_compte de enfant sous condition
    enfant.prise_en_charge sous forme
      ServicesSociauxAllocationVerséeAuxServicesSociaux
  conséquence égal à PriseEnCompte.Zéro

  définition versement de enfant sous condition
    enfant.prise_en_charge sous forme
      ServicesSociauxAllocationVerséeAuxServicesSociaux
  conséquence égal à VersementAllocations.AllocationVerséeAuxServicesSociaux

  définition prise_en_compte de enfant sous condition
    enfant.prise_en_charge sous forme
      ServicesSociauxAllocationVerséeÀLaFamille
  conséquence égal à PriseEnCompte.Complète


  définition versement de enfant sous condition
    enfant.prise_en_charge sous forme
      ServicesSociauxAllocationVerséeÀLaFamille
  conséquence égal à VersementAllocations.Normal
```

Un décret en Conseil d'Etat fixe les conditions d'application du présent
article, notamment dans les cas énumérés ci-dessous :

a) retrait total de l'autorité parentale des parents ou de l'un d'eux ;

b) indignité des parents ou de l'un d'eux ;

c) divorce, séparation de corps ou de fait des parents ;

d) enfants confiés à un service public, à une institution privée, à un particulier.

```catala
# Ce programme ne cherche pas à formaliser les détails pour lesquels un enfant
# est confié à un service social.
```

###### Article L521-3|LEGIARTI000006743289

Chacun des enfants à charge, à l'exception du plus âgé, ouvre droit à partir
d'un âge minimum à une majoration des allocations familiales.

```catala
champ d'application AllocationsFamiliales :
  règle droit_ouvert_majoration de enfant
  sous condition
    (non (est_enfant_le_plus_âgé de enfant)) et
    (enfant.âge >= âge_minimum_alinéa_1_l521_3 de enfant)
  conséquence rempli
```

Toutefois, les personnes ayant un nombre déterminé d'enfants à charge
bénéficient de ladite majoration pour chaque enfant à charge à partir
de l'âge mentionné au premier alinéa.

```catala
champ d'application AllocationsFamiliales :
  exception
  règle droit_ouvert_majoration de enfant
  sous condition
    (nombre de enfants_à_charge_droit_ouvert_prestation_familiale >=
      nombre_enfants_alinéa_2_l521_3) et
    (enfant.âge >= âge_minimum_alinéa_1_l521_3 de enfant)
  conséquence rempli
```

#### Titre 5 : Dispositions communes

##### Chapitre 1er : Etablissement du salaire de base

###### Article L551-1|LEGIARTI000031688371

Le montant des prestations familiales est déterminé d'après des bases
mensuelles de calcul revalorisées au 1er avril de chaque année par application
du coefficient mentionné à l'article L. 161-25 .

```catala
# Idem que L521-1, on ne formalise pas ici l'évolution de la BMPA
champ d'application PrestationsFamiliales :
  assertion fixé prestations_familiales.base_mensuelle par décret
```

### Livre 7 : Régimes divers - Dispositions diverses

#### Titre 5 : Dispositions particulières à la Guadeloupe, à la Guyane, à la Martinique, à La Réunion, à Saint-Barthélemy et à Saint-Martin

##### Chapitre 1er : Généralités

###### Article L751-1|LEGIARTI000031323778

Les dispositions du présent titre s'appliquent en Guadeloupe, en Guyane,
en Martinique, à La Réunion, à Saint-Barthélemy et à Saint-Martin à
l'ensemble des bénéficiaires de la législation générale de sécurité
sociale, y compris les membres des professions agricoles.

```catala
# Angle mort: ici nous n'avons pas formalisé la partie sur la législation
# générale de sécurité sociale et les membres des professions agricoles.

champ d'application PrestationsFamiliales :
  règle régime_outre_mer_l751_1 sous condition
    (résidence = Guadeloupe) ou
    (résidence = Guyane) ou
    (résidence = Martinique) ou
    (résidence = LaRéunion) ou
    (résidence = SaintBarthélemy) ou
    (résidence = SaintMartin)
  conséquence rempli
```

##### Chapitre 5 : Prestations familiales et prestations assimilées

###### Article L755-3|LEGIARTI000033728786

Les dispositions des articles L. 512-1 à L. 512-4 , L. 513-1 , L. 521-2 ,
L. 552-1 , L. 553-1 , L. 553-2 , L. 553-4 , L. 582-1 , L. 582-2 , L. 583-3
et L. 583-5 sont applicables aux collectivités mentionnées à l'article L. 751-1 .

La base de calcul des prestations familiales est la même que celle qui est
fixée en application de l'article L. 551-1 .

```catala
# Aucun changement dans le code, puisque les articles restent applicables
```

###### Article L755-11|LEGIARTI000031323803

Les conditions d'attribution des allocations familiales et de leurs majorations
fixées par les articles L. 521-1 et L. 521-3 sont applicables dans les
collectivités mentionnées à l'article L. 751-1.

```catala
# Aucun changement dans le code, puisque les articles restent applicables
```

Toutefois, les dispositions de l'article L. 755-12 restent en vigueur aussi
longtemps que le présent chapitre V est applicable.

###### Article L755-12|LEGIARTI000029962999

Les allocations familiales sont dues, pour tout enfant, à la personne qui a
effectivement la charge de celui-ci.

```catala
# TODO Liane: Angle mort, ici c'est marqué "effectivement la charge" mais dans l'article de
# L521-2 il faut charge effective ET permanente. Pourquoi cette différence ?
# Quelles conséquences pratiques ?
# Apparemment en 1987 il y avait encore des exceptions pour certains agents
# publics d'outre-mer

champ d'application AllocationsFamiliales:
  exception
  règle droit_ouvert_base sous condition
    prestations_familiales.régime_outre_mer_l751_1 et
    (nombre de enfants_à_charge_droit_ouvert_prestation_familiale >= 1)
  conséquence rempli
```
Toutefois, les quatre derniers alinéas de l'article L. 521-1 ne sont pas
applicables lorsque le ménage ou la personne a un seul enfant à charge.

```catala
# Ceci concerne l'ouverture du droit à l'allocation forfaitaire
# et au complément dégressif.

champ d'application AllocationsFamiliales :
  exception
  règle droit_ouvert_forfaitaire de enfant sous condition
     prestations_familiales.régime_outre_mer_l751_1 et
     (nombre de enfants_à_charge_droit_ouvert_prestation_familiale = 1)
  conséquence non rempli

  exception
  règle droit_ouvert_complément sous condition
    prestations_familiales.régime_outre_mer_l751_1 et
    (nombre de enfants_à_charge_droit_ouvert_prestation_familiale = 1)
  conséquence non rempli
```
## Partie réglementaire - Décrets en Conseil d'Etat

### Livre 5 : Prestations familiales et prestations assimilées

#### Titre 1 : Champ d'application - Généralités

##### Chapitre 2 : Champ d'application.

###### Article R512-2|LEGIARTI000006750602

Les enfants ouvrent droit aux prestations familiales jusqu'à l'âge de vingt
ans sous réserve que leur rémunération n'excède pas le plafond fixé au deuxième
alinéa du présent article.

```catala
# Attention: ici la condition de ressources au dessous du plafond est une répétition
# par rapport au texte de L512-3. On ne remet donc pas le code ici correspondant
# à cette limitation.

champ d'application PrestationsFamiliales :
  définition âge_l512_3_2 égal à 20
```

Le plafond de rémunération mentionné au 2° de l'article L. 512-3 est égal, pour
un mois, à 55 % du salaire minimum interprofessionnel de croissance défini aux
articles L. 141-1 à L. 141-9 du code du travail, multiplié par 169.


```catala
champ d'application PrestationsFamiliales :
  définition plafond_l512_3_2 égal à
   (smic.brut_horaire *€ 55 %) *€ 169,0
```

Pour ceux des enfants qui bénéficient d'avantages en nature, l'évaluation de ces
avantages devra être faite suivant les barèmes fixés pour l'application de la
législation sur les assurances sociales.


```catala
# Le programme Catala ne tient pas en compte des avantages en nature.
# TODO: Liane, faire de la recherche juridique sur le sujet.
```

#### Titre 2 : Prestations générales d'entretien

##### Chapitre 1er : Allocations familiales

###### Article R521-1|LEGIARTI000018735853
L'âge mentionné au premier alinéa de l'article L. 521-3 à partir duquel les
enfants ouvrent droit à la majoration des allocations familiales est fixé à
14 ans.

```catala
champ d'application AllocationsFamiliales :
  définition âge_minimum_alinéa_1_l521_3 de enfant égal à 14
```

Le nombre minimum d'enfants à charge, mentionné au deuxième alinéa de l'article
L. 521-3 ouvrant droit à ladite majoration pour chaque enfant est fixé à trois.

```catala
champ d'application AllocationsFamiliales :
  définition nombre_enfants_alinéa_2_l521_3 égal à 3
```

NOTA : Décret n° 2008-409 du 28 avril 2008 JORF du 29 avril 2008 art. 2 : Les
modifications induites par le décret n° 2008-409 s'appliquent aux enfants dont
le onzième anniversaire est postérieur au 30 avril 2008.

```catala
# Notons ici un champ d'application différent, correspondant à une ancienne
# version du corpus législatif dont un morceau s'applique encore. Nous avons
# choisi de montrer ce vieux champ d'application ici plutôt qu'à côté du texte
# du décret de 2008 pour des raisons de place seulement.
champ d'application AllocationFamilialesAvril2008:
  # Âge limite avant décret n° 2008-409 du 28 avril 2008
  définition âge_minimum_alinéa_1_l521_3 égal à 16

champ d'application AllocationsFamiliales :
  exception
  définition âge_minimum_alinéa_1_l521_3 de enfant
  sous condition (enfant.date_de_naissance +@ 11 an <=@ |2008-04-30|)
  conséquence égal à
    version_avril_2008.âge_minimum_alinéa_1_l521_3
```

###### Article R521-2|LEGIARTI000006750608

Dans les situations visées au deuxième alinéa de l'article L. 521-2 ,
l'allocataire est celui des deux parents qu'ils désignent d'un commun accord.
A défaut d'accord sur la désignation d'un allocataire unique, chacun des deux
parents peut se voir reconnaître la qualité d'allocataire :

1° Lorsque les deux parents en ont fait la demande conjointe ;

2° Lorsque les deux parents n'ont ni désigné un allocataire unique, ni fait
une demande conjointe de partage.

```catala
# Le fait que l'allocataire dans le cas d'un commun accord soit celui des
# deux parents désigné par l'accord est déjà modélisé dans L521-2.
```

Lorsque les parents ont désigné un allocataire unique ou fait une demande
conjointe de partage, ils ne peuvent remettre en cause les modalités ainsi
choisies qu'au bout d'un an, sauf modification des modalités de résidence du
ou des enfants.

```catala
# On ne modélise pas le délai minimal entre changement de situation dans
# ce programme.
```

###### Article R521-3|LEGIARTI000006750610

Sous réserve de l'article R. 521-4, dans les situations visées aux 1° et 2° de
l'article R. 521-2 , la prestation due à chacun des parents est égale au
montant des allocations familiales dues pour le total des enfants à charge,
multiplié par un coefficient résultant du rapport entre le nombre moyen
d'enfants et le nombre total d'enfants.

```catala
champ d'application AllocationsFamiliales :
  définition montant_avec_garde_alternée_base égal à
    montant_initial_base *€ rapport_enfants_total_moyen

  définition rapport_enfants_total_moyen égal à
    si nombre_total_enfants = 0,0 alors 0,0 sinon
      (nombre_moyen_enfants /. nombre_total_enfants)
```

Le nombre moyen d'enfants, pour chaque foyer, est obtenu en faisant la somme
du nombre d'enfants à charge dans les conditions suivantes :

1° Chaque enfant en résidence alternée compte pour 0,5 ;

2° Les autres enfants à charge comptent pour 1.

```catala
champ d'application AllocationsFamiliales :
  définition nombre_moyen_enfants égal à
    somme décimal pour enfant dans
      enfants_à_charge_droit_ouvert_prestation_familiale
    de (
      selon (prise_en_compte de enfant) sous forme
      -- Complète : 1,0
      -- Partagée : 0,5
      # Pas directement dans cet article de loi, mais concerne le
      # cas où l'enfant est pris en charge par les services sociaux et
      # la part de l'allocation va également au service social
      -- Zéro: 0,0
    )
```

Le nombre total d'enfants, pour chaque foyer, est obtenu en faisant la somme du
ou des enfants en résidence alternée et, le cas échéant, du ou des autres
enfants à charge.

```catala
champ d'application AllocationsFamiliales :
  définition nombre_total_enfants égal à
    entier_vers_décimal de (nombre de
      enfants_à_charge_droit_ouvert_prestation_familiale)
```

###### Article R521-4|LEGIARTI000006750613

Pour l'ouverture du droit à la majoration prévue à l'article L. 521-3 , le
nombre d'enfants à charge est évalué dans les conditions prévues au premier
alinéa de l'article R. 521-3.

```catala
# Le renvoi ci-dessus ne fait qu'annoncer qu'on va appliquer un système
# de décote pour garde alternée au système de majorations.
# Ce renvoi signifie que notre liste enfants_à_charge_droit_ouvert_prestation_familiale
# qui était utilisée pour la prestation de base est la même que la liste
# d'enfant utilisée pour le calcul des majorations
```

Lorsque le ou les enfants ouvrant droit à ladite majoration sont en résidence
alternée, le montant servi au titre de cette majoration est réduit de moitié.

```catala
champ d'application AllocationsFamiliales :
  définition
    montant_avec_garde_alternée_majoration de enfant
  égal à montant_initial_majoration de enfant *€ (
    selon (prise_en_compte de enfant) sous forme
    -- Complète : 100%
    -- Partagée : 50%
    # Pas directement dans cet arricle de loi, mais concerne le
    # cas où l'enfant est pris en charge par les services sociaux et
    # la part de l'allocation va également au service social
    -- Zéro: 0%
  )
```

### Livre 7 : Régimes divers - Dispositions diverses

#### Titre 5 : Départements d'outre-mer

##### Chapitre 5 : Prestations familiales et prestations assimilées

###### Article R755-0-2|LEGIARTI000006752633

Le plafond de rémunération des enfants à charge mentionnés à l'article
L. 512-3 est égal, pour un mois, à 55 % du salaire minimum de croissance en
vigueur dans chacun des départements mentionnés à l'article L. 751-1 ,
multiplié par 169.

```catala
champ d'application PrestationsFamiliales :
  exception
  définition plafond_l512_3_2
  sous condition régime_outre_mer_l751_1
  conséquence égal à
    (smic.brut_horaire *€ 55%) *€ 169,0
```
## Partie réglementaire - Décrets simples

#### Livre 5 : Prestations familiales et prestations assimilées

##### Titre 2 : Prestations générales d'entretien

###### Chapitre 1er : Allocations familiales

####### Article D521-1|LEGIARTI000030680318

I.-Pour l'application de l'article L. 521-1 , le montant des allocations
familiales et de la majoration pour âge prévue à l'article L. 521-3
est défini selon le barème suivant :

```catala
# Composantes des allocations familiales
champ d'application AllocationsFamiliales :
  définition montant_initial_base égal à
    montant_initial_base_deuxième_enfant +€
    montant_initial_base_troisième_enfant_et_plus

  définition montant_initial_majoration de enfant égal à
    montant_initial_métropole_majoration de enfant
```

1° Lorsque le ménage ou la personne a disposé d'un montant de ressources
inférieur ou égal au plafond défini au I de l'article D. 521-3, les taux
servant au calcul des allocations familiales sont fixés, en pourcentage
de la base mensuelle prévue à l'article L. 551-1, à :
a) 32 % pour le deuxième enfant à charge ;
b) 41 % pour le troisième enfant à charge et chacun des suivants.

```catala
champ d'application AllocationsFamiliales sous condition
  ressources_ménage <=€ plafond_I_d521_3:

  étiquette base
  définition montant_initial_base_deuxième_enfant égal à
    si nombre de enfants_à_charge_droit_ouvert_prestation_familiale > 1
    alors bmaf.montant *€ 32 %
    sinon 0 €

  définition montant_initial_base_troisième_enfant_et_plus égal à
    si nombre de enfants_à_charge_droit_ouvert_prestation_familiale > 2
    alors (bmaf.montant *€ 41 %) *€ (
      entier_vers_décimal de
        ((nombre de enfants_à_charge_droit_ouvert_prestation_familiale) - 2)
    ) sinon 0 €
```

La majoration pour âge est fixée à 16 % de la base mensuelle de calcul des
prestations familiales ;

```catala
champ d'application AllocationsFamiliales sous condition
  ressources_ménage <=€ plafond_I_d521_3:

  définition montant_initial_métropole_majoration de enfant
  sous condition
    droit_ouvert_majoration de enfant
  conséquence égal à
    bmaf.montant *€ 16 %
```

2° Lorsque le ménage ou la personne a disposé d'un montant de ressources
supérieur au plafond défini au I de l'article D. 521-3 et inférieur ou égal
à celui défini au II du même article, les taux servant au calcul des
allocations familiales sont fixés, en pourcentage de la base mensuelle
prévue à l'article L. 551-1, à :
a) 16 % pour le deuxième enfant à charge ;
b) 20,5 % pour le troisième enfant à charge et chacun des suivants.

```catala
champ d'application AllocationsFamiliales sous condition
  (ressources_ménage >€ plafond_I_d521_3) et
  (ressources_ménage <=€ plafond_II_d521_3):

  étiquette base
  définition montant_initial_base_deuxième_enfant égal à
    si nombre de enfants_à_charge_droit_ouvert_prestation_familiale > 1
    alors bmaf.montant *€ 16 %
    sinon 0 €

  définition montant_initial_base_troisième_enfant_et_plus égal à
    si nombre de enfants_à_charge_droit_ouvert_prestation_familiale > 2
    alors (bmaf.montant *€ 20,5 %) *€ (
      entier_vers_décimal de
        ((nombre de enfants_à_charge_droit_ouvert_prestation_familiale) - 2)
    ) sinon 0 €
```

La majoration pour âge est fixée à 8 % de la base mensuelle de calcul des
prestations familiales ;

```catala
champ d'application AllocationsFamiliales sous condition
  (ressources_ménage >€ plafond_I_d521_3) et
  (ressources_ménage <=€ plafond_II_d521_3):

  définition montant_initial_métropole_majoration de enfant sous condition
    droit_ouvert_majoration de enfant
  conséquence égal à
    bmaf.montant *€ 8 %
```

3° Lorsque le ménage ou la personne a disposé d'un montant de ressources
supérieur au plafond défini au II de l'article D. 521-3, les taux servant
au calcul des allocations familiales sont fixés, en pourcentage de la base
mensuelle prévue à l'article L. 551-1, à :
a) 8 % pour le deuxième enfant à charge ;
b) 10,25 % pour le troisième enfant à charge et chacun des suivants.

```catala
champ d'application AllocationsFamiliales sous condition
  ressources_ménage >€ plafond_II_d521_3:
  étiquette base
  définition montant_initial_base_deuxième_enfant égal à
    si nombre de enfants_à_charge_droit_ouvert_prestation_familiale > 1
    alors bmaf.montant *€ 8 %
    sinon 0 €

  définition montant_initial_base_troisième_enfant_et_plus égal à
    si nombre de enfants_à_charge_droit_ouvert_prestation_familiale > 2
    alors (bmaf.montant *€ 10,25 %) *€ (
      entier_vers_décimal de
        ((nombre de enfants_à_charge_droit_ouvert_prestation_familiale) - 2)
    ) sinon 0 €
```

La majoration pour âge est fixée à 4 % de la base mensuelle de calcul des
prestations familiales.

```catala
champ d'application AllocationsFamiliales sous condition
  ressources_ménage >€ plafond_II_d521_3:

  définition montant_initial_métropole_majoration de enfant sous condition
    droit_ouvert_majoration de enfant
  conséquence égal à
    bmaf.montant *€ 4 %
```

II.-En application du sixième alinéa de l'article L. 521-1, le montant
mensuel des allocations familiales et, le cas échéant, de la majoration
pour âge est majoré d'un complément dégressif, lorsque les ressources
annuelles du ménage ou de la personne dépassent l'un des plafonds défini
au I ou au II de l'article D. 521-3 d'une somme inférieure à douze fois
le montant mensuel des allocations familiales augmenté, le cas échéant,
de la ou des majorations pour âge.

Ce complément dégressif est égal, pour chaque mois, au douzième de la
différence entre, d'une part, ce plafond de ressources majoré de la somme
définie à l'alinéa précédent et, d'autre part, le montant des ressources.

```catala
champ d'application AllocationsFamiliales :

  # Attention : cette formule ne marche que si la diminution des
  # allocations se fait d'un facteur 1/2 à chaque passage de plafond de
  # ressources.

  exception
  définition complément_dégressif de allocation
  sous condition
    (ressources_ménage >€ plafond_I_d521_3) et
    (ressources_ménage <=€ plafond_I_d521_3 +€ allocation *€ 12,0 )
  conséquence égal à
    (plafond_I_d521_3 +€ allocation *€ 12,0 -€ ressources_ménage) *€ (1,0 /. 12,0)

  exception
  définition complément_dégressif de allocation
  sous condition
    (ressources_ménage >€ plafond_II_d521_3) et
    (ressources_ménage <=€ plafond_II_d521_3 +€ allocation *€ 12,0)
  conséquence égal à
    (plafond_II_d521_3 +€ allocation *€ 12,0 -€ ressources_ménage) *€ (1,0 /. 12,0)

  # Dans les autres cas, le dépassement est nul
  définition complément_dégressif de allocations égal à 0 €

  définition montant_versé_complément_pour_base_et_majoration égal à
    si droit_ouvert_complément alors
      (complément_dégressif de
        montant_base_complément_pour_base_et_majoration)
    sinon 0 €

  définition montant_base_complément_pour_base_et_majoration égal à
    montant_versé_base +€ montant_versé_majoration
```

####### Article D521-2|LEGIARTI000030680324

```catala
# L'allocation forfaitaire totale est la somme de toutes les allocations
# forfaitaires distribuées à chaque enfant du ménage
champ d'application AllocationsFamiliales :
  définition montant_versé_forfaitaire égal à
    montant_versé_forfaitaire_par_enfant *€
    entier_vers_décimal de (nombre pour enfant dans enfants_à_charge de
      droit_ouvert_forfaitaire de enfant)
```

I.-Le montant mensuel de l'allocation forfaitaire prévue au deuxième alinéa de
l'article L. 521-1 est défini selon le barème suivant :

1° Lorsque le ménage ou la personne a disposé d'un montant de ressources
inférieur ou égal au plafond défini au I de l'article D. 521-3, le montant
mensuel de l'allocation forfaitaire est fixé à 20,234 % de la base mensuelle
de calcul des allocations familiales par enfant ;

```catala
# TODO Liane: la syntaxe de la phrase est très ambiguë, on a l'impression que
# c'est la BMAF qui est "par enfant" et pas le montant de l'allocation forfaitaire
# Erreur de rédaction ? Problème juridique ?

champ d'application AllocationsFamiliales :
  définition montant_versé_forfaitaire_par_enfant sous condition
    ressources_ménage <=€ plafond_I_d521_3
  conséquence égal à
    (bmaf.montant *€ 20,234 %)
```

2° Lorsque le ménage ou la personne a disposé d'un montant de ressources
supérieur au plafond défini au I de l'article D. 521-3 et inférieur ou égal
à celui défini au II du même article, le montant mensuel de l'allocation
forfaitaire est fixé à 10,117 % de la base mensuelle de calcul des allocations
familiales par enfant ;

```catala
champ d'application AllocationsFamiliales :
  définition montant_versé_forfaitaire_par_enfant sous condition
    (ressources_ménage >€ plafond_I_d521_3) et
    (ressources_ménage <=€ plafond_II_d521_3)
  conséquence égal à
    (bmaf.montant *€ 10,117 %)
```

3° Lorsque le ménage ou la personne a disposé d'un montant de ressources
supérieur au plafond défini au II de l'article D. 521-3, le montant mensuel
de l'allocation forfaitaire est fixé à 5,059 % de la base mensuelle de calcul
des allocations familiales par enfant.

```catala
champ d'application AllocationsFamiliales :
  définition montant_versé_forfaitaire_par_enfant sous condition
    ressources_ménage >€ plafond_II_d521_3
  conséquence égal à
    (bmaf.montant *€ 5,059 %)
```

II.-En application du sixième alinéa de l'article L. 521-1, le montant mensuel
de l'allocation forfaitaire est majoré d'un complément dégressif lorsque les
ressources annuelles du ménage ou de la personne dépassent l'un des plafonds
défini au I ou au II de l'article D. 521-3 d'une somme inférieure à douze fois
le montant mensuel de l'allocation forfaitaire auquel l'enfant ouvre droit.
Ce complément dégressif est égal, pour chaque mois, au douzième de la différence
entre, d'une part, ce plafond de ressources majoré de la somme définie à
l'alinéa précédent et, d'autre part, le montant des ressources.

```catala
champ d'application AllocationsFamiliales :
  exception
  définition montant_versé_complément_pour_forfaitaire
  sous condition
    (ressources_ménage >€ plafond_I_d521_3) et
    (ressources_ménage <=€ plafond_I_d521_3 +€
      montant_versé_forfaitaire *€ 12,0 )
  conséquence égal à
    (plafond_I_d521_3 +€ montant_versé_forfaitaire *€ 12,0
      -€ ressources_ménage) *€ (1,0 /. 12,0)

  exception
  définition montant_versé_complément_pour_forfaitaire
  sous condition
    (ressources_ménage >€ plafond_II_d521_3) et
    (ressources_ménage <=€ plafond_II_d521_3 +€
      montant_versé_forfaitaire *€ 12,0)
  conséquence égal à
    (plafond_II_d521_3 +€ montant_versé_forfaitaire *€ 12,0
      -€ ressources_ménage) *€ (1,0 /. 12,0)

  # Dans les autres cas, le dépassement est nul
  définition montant_versé_complément_pour_forfaitaire égal à 0 €
```

III.-Le nombre minimum d'enfants à charge mentionné au deuxième alinéa de
l'article L. 521-1 est fixé à trois.

```catala
champ d'application AllocationsFamiliales :
  définition nombre_enfants_l521_1 égal à 3
```

####### Article D521-3|LEGIARTI000030678079

I.-Le plafond prévu au 1° du I des articles D. 521-1 et D. 521-2 est fixé à
55 950 euros. Il est majoré de 5 595 euros par enfant à charge.

```catala
champ d'application AllocationsFamiliales :
  définition plafond_I_d521_3 égal à 55 950 € +€
    5 595 € *€ (entier_vers_décimal de
      (nombre de enfants_à_charge_droit_ouvert_prestation_familiale))
```

II.-Le plafond prévu au 2° du I des articles D. 521-1 et D. 521-2 est fixé à
78 300 euros. Il est majoré de 5 595 euros par enfant à charge.

```catala
champ d'application AllocationsFamiliales :
  définition plafond_II_d521_3 égal à 78 300 € +€
    5 595 € *€ (entier_vers_décimal de
      (nombre de enfants_à_charge_droit_ouvert_prestation_familiale))
```

III.-Les montants des plafonds et de leur majoration respective fixés au présent
article sont revalorisés au 1er janvier de chaque année, conformément à
l'évolution en moyenne annuelle des prix à la consommation hors tabac de
l'année civile de référence, par arrêté des ministres chargés de la sécurité
sociale, du budget et de l'agriculture.

```catala
# Nota : ces montants sont en réalités remis à jour chaque année par des décrets,
# voir decrets_divers.catala_fr
```

#### Livre 7 : Régimes divers - Dispositions diverses

##### Titre 5 : Départements d'outre-mer

###### Chapitre 5 : Prestations familiales et prestations assimilées

####### Article D755-5|LEGIARTI000006738575

I. - Les taux servant au calcul des allocations familiales et de la majoration
prévue à l'article L. 755-11 sont identiques à ceux mentionnés à l'article
D. 521-1.

```catala
# Pas de changement à déclarer
```

II. - En application de l'article L. 755-11, 2e alinéa, le taux servant au
calcul des allocations familiales servies pour un seul enfant à charge est
fixé à 5,88 p. 100 de la base mensuelle prévue à l'article L. 755-3.

```catala
# Composantes des allocations familiales
champ d'application AllocationsFamiliales :

  exception
  définition montant_initial_base sous condition
    prestations_familiales.régime_outre_mer_l751_1 et
    nombre de enfants_à_charge_droit_ouvert_prestation_familiale = 1
  conséquence égal à
    montant_initial_base_premier_enfant

  # Valeur par défaut
  étiquette base définition montant_initial_base_premier_enfant égal à 0€

  exception base définition montant_initial_base_premier_enfant sous condition
    prestations_familiales.régime_outre_mer_l751_1 et
    (nombre de enfants_à_charge_droit_ouvert_prestation_familiale = 1)
  conséquence égal à
    bmaf.montant *€ 5,88 %
```
informatique
La majoration des allocations familiales pour un seul enfant à charge est
fixée à 3,69 p. 100 de la base mensuelle prévue à l'article L. 755-3 à
partir de onze ans et à 5,67 p. 100 à partir de seize ans.

```catala
champ d'application AllocationsFamiliales :
 exception définition montant_initial_majoration de enfant
  sous condition
    (droit_ouvert_majoration de enfant) et
    prestations_familiales.régime_outre_mer_l751_1 et
    (nombre de enfants_à_charge_droit_ouvert_prestation_familiale = 1) et
    (enfant.âge >= 11 et enfant.âge < 16)
  conséquence égal à
    bmaf.montant *€ 3,69 %

 exception définition montant_initial_majoration de enfant
  sous condition
    (droit_ouvert_majoration de enfant) et
    prestations_familiales.régime_outre_mer_l751_1 et
    (nombre de enfants_à_charge_droit_ouvert_prestation_familiale = 1) et
    (enfant.âge >= 16)
  conséquence égal à
    bmaf.montant *€ 5,67 %
```
# Épilogue

## Règles diverses

Les textes législatifs et règlementaires contiennent toutes les informations
kécessaires au calcul. Cependant, certaines règles de calcul sont implicites
dans ces textes et doivent être explicitées pour le calcul informatique. C'est
la fonction du bloc de code ci-dessous.

```catala-metadata
champ d'application EnfantLePlusÂgé:
  définition le_plus_âgé  égal à
    contenu maximum entier initial Enfant {
      -- identifiant: -1
      -- obligation_scolaire: Pendant
      -- rémuneration_mensuelle: 0€
      -- date_de_naissance: |1900-01-01|
      -- âge: 0
      -- prise_en_charge: EffectiveEtPermanente
      -- a_déjà_ouvert_droit_aux_allocations_familiales: faux
      -- bénéficie_titre_personnel_aide_personnelle_logement: faux
    }
    pour potentiel_plus_âgé dans enfants de potentiel_plus_âgé.âge


champ d'application AllocationsFamiliales:
  définition montant_initial_métropole_majoration de enfant sous condition
    non (droit_ouvert_majoration de enfant)
  conséquence égal à 0 €

  règle droit_ouvert_complément rempli

  définition enfant_le_plus_âgé.enfants égal à enfants_à_charge
  définition est_enfant_le_plus_âgé de enfant égal à
    enfant_le_plus_âgé.le_plus_âgé = enfant

  définition montant_versé_base égal à
    si droit_ouvert_base alors montant_avec_garde_alternée_base sinon 0€
  définition montant_versé_majoration égal à
    si droit_ouvert_base alors
      somme argent pour enfant dans enfants_à_charge de
        montant_avec_garde_alternée_majoration de enfant
    sinon 0€

  définition montant_versé égal à
    si droit_ouvert_base alors
      montant_versé_base +€
      montant_versé_majoration +€
      montant_versé_forfaitaire +€
      montant_versé_complément_pour_base_et_majoration +€
      montant_versé_complément_pour_forfaitaire
    sinon 0€
```

## Interface du programme

Le calcul des allocations familiales nécessite un certain nombre d'informations
sur les enfants à charge. Certaines de ces informations sont dépendantes les
unes des autres : par exemple, l'âge de l'enfant se déduit de sa date de
naissance. Le but du block ci-dessous est d'encoder ces dépendances, de telle
sorte que l'utilisateur puisse ne doive saisir que le nombre minimal
d'informations sur les enfants à charge.

```catala-metadata
déclaration structure EnfantEntrée:
  donnée d_identifiant contenu entier
  donnée d_rémuneration_mensuelle contenu argent
  donnée d_date_de_naissance contenu date
  donnée d_prise_en_charge contenu PriseEnCharge
  donnée d_a_déjà_ouvert_droit_aux_allocations_familiales contenu booléen
  donnée d_bénéficie_titre_personnel_aide_personnelle_logement contenu booléen

déclaration champ d'application InterfaceAllocationsFamiliales:
  entrée i_date_courante contenu date
  entrée i_enfants contenu collection EnfantEntrée
  interne enfants_à_charge contenu collection Enfant
  allocations_familiales champ d'application AllocationsFamiliales
  entrée i_ressources_ménage contenu argent
  entrée i_résidence contenu Collectivité
  sortie i_montant_versé contenu argent
  entrée i_personne_charge_effective_permanente_est_parent condition
  entrée i_personne_charge_effective_permanente_remplit_titre_I condition
  entrée i_avait_enfant_à_charge_avant_1er_janvier_2012 condition

champ d'application InterfaceAllocationsFamiliales:
  définition allocations_familiales.date_courante égal à i_date_courante
  définition allocations_familiales.enfants_à_charge égal à enfants_à_charge
  définition allocations_familiales.ressources_ménage égal à i_ressources_ménage
  définition allocations_familiales.résidence égal à i_résidence
  définition i_montant_versé égal à allocations_familiales.montant_versé
  règle
    allocations_familiales.personne_charge_effective_permanente_est_parent
  sous condition i_personne_charge_effective_permanente_est_parent
  conséquence rempli
  règle
    allocations_familiales.personne_charge_effective_permanente_remplit_titre_I
  sous condition i_personne_charge_effective_permanente_remplit_titre_I
  conséquence rempli
  règle
    allocations_familiales.avait_enfant_à_charge_avant_1er_janvier_2012
  sous condition i_avait_enfant_à_charge_avant_1er_janvier_2012
  conséquence rempli
```

### Code de l'éducation

### Article L131-1|LEGIARTI000038901859

L'instruction est obligatoire pour chaque enfant dès l'âge de trois ans et
jusqu'à l'âge de seize ans.

La présente disposition ne fait pas obstacle à l'application des prescriptions
particulières imposant une scolarité plus longue.

```catala
champ d'application InterfaceAllocationsFamiliales:
  définition enfants_à_charge égal à application pour enfant dans i_enfants de
    Enfant {
      -- identifiant : enfant.d_identifiant
      -- rémuneration_mensuelle : enfant.d_rémuneration_mensuelle
      -- date_de_naissance : enfant.d_date_de_naissance
      -- prise_en_charge : enfant.d_prise_en_charge
      -- âge: accès_année de
        (|0000-01-01| +@ (i_date_courante -@ enfant.d_date_de_naissance))
      -- obligation_scolaire :
        (si enfant.d_date_de_naissance +@ 3 an >=@ i_date_courante alors
          Avant
        sinon (si enfant.d_date_de_naissance +@ 16 an >=@ i_date_courante alors
          Pendant
        sinon Après))
      -- a_déjà_ouvert_droit_aux_allocations_familiales:
        enfant.d_a_déjà_ouvert_droit_aux_allocations_familiales
      -- bénéficie_titre_personnel_aide_personnelle_logement:
        enfant.d_bénéficie_titre_personnel_aide_personnelle_logement
    }
```

# Tests

```catala
déclaration champ d'application Données:
  sortie enfant1 contenu EnfantEntrée
  sortie enfant2 contenu EnfantEntrée
  sortie enfant3 contenu EnfantEntrée
  sortie enfant4 contenu EnfantEntrée
  sortie enfant5 contenu EnfantEntrée

champ d'application Données:
  définition enfant1 égal à EnfantEntrée {
    -- d_identifiant: 1
    -- d_date_de_naissance: |2007-01-01|
    -- d_rémuneration_mensuelle: 0€
    -- d_prise_en_charge: EffectiveEtPermanente
    -- d_a_déjà_ouvert_droit_aux_allocations_familiales: vrai
    -- d_bénéficie_titre_personnel_aide_personnelle_logement: faux
  }
  définition enfant2 égal à EnfantEntrée {
    -- d_identifiant: 2
    -- d_date_de_naissance: |2009-01-01|
    -- d_rémuneration_mensuelle: 0€
    -- d_prise_en_charge: EffectiveEtPermanente
    -- d_a_déjà_ouvert_droit_aux_allocations_familiales: vrai
    -- d_bénéficie_titre_personnel_aide_personnelle_logement: faux
  }
  définition enfant3 égal à EnfantEntrée {
    -- d_identifiant: 3
    -- d_date_de_naissance: |2003-01-01|
    -- d_rémuneration_mensuelle: 400€
    -- d_prise_en_charge: GardeAlternéePartageAllocations
    -- d_a_déjà_ouvert_droit_aux_allocations_familiales: vrai
    -- d_bénéficie_titre_personnel_aide_personnelle_logement: faux
  }
  définition enfant4 égal à EnfantEntrée {
    -- d_identifiant: 4
    -- d_date_de_naissance: |2001-01-01|
    -- d_rémuneration_mensuelle: 1100€
    -- d_prise_en_charge: EffectiveEtPermanente
    -- d_a_déjà_ouvert_droit_aux_allocations_familiales: vrai
    -- d_bénéficie_titre_personnel_aide_personnelle_logement: faux
  }
  définition enfant5 égal à EnfantEntrée {
    -- d_identifiant: 5
    -- d_date_de_naissance: |2000-01-01|
    -- d_rémuneration_mensuelle: 0€
    -- d_prise_en_charge: EffectiveEtPermanente
    -- d_a_déjà_ouvert_droit_aux_allocations_familiales: vrai
    -- d_bénéficie_titre_personnel_aide_personnelle_logement: faux
  }

déclaration champ d'application Test1:
  f champ d'application InterfaceAllocationsFamiliales
  données champ d'application Données

champ d'application Test1:
  définition f.i_enfants égal à
    [données.enfant1;données.enfant2;données.enfant3;données.enfant4]
  définition f.i_ressources_ménage égal à 30 000 €
  définition f.i_date_courante égal à |2020-05-01|
  définition f.i_résidence égal à Métropole
  règle f.i_personne_charge_effective_permanente_est_parent rempli
  assertion f.i_montant_versé = 351,54€

déclaration champ d'application Test2:
  f champ d'application InterfaceAllocationsFamiliales
  données champ d'application Données

champ d'application Test2:
  définition f.i_enfants égal à
    [données.enfant1;données.enfant2;données.enfant5]
  définition f.i_ressources_ménage égal à 30 000 €
  définition f.i_date_courante égal à |2020-05-01|
  définition f.i_résidence égal à Métropole
  règle f.i_personne_charge_effective_permanente_est_parent rempli
  assertion f.i_montant_versé = 216,46€


déclaration champ d'application Test3:
  f champ d'application InterfaceAllocationsFamiliales
  données champ d'application Données

champ d'application Test3:
  définition f.i_enfants égal à [données.enfant1]
  définition f.i_ressources_ménage égal à 63540 €
  définition f.i_date_courante égal à |2020-05-01|
  définition f.i_résidence égal à Guyane
  règle f.i_personne_charge_effective_permanente_est_parent rempli
  assertion f.i_montant_versé = 24,37 €

déclaration champ d'application Test4:
  f champ d'application InterfaceAllocationsFamiliales
  données champ d'application Données

champ d'application Test4:
  définition f.i_enfants égal à [données.enfant1; données.enfant3]
  définition f.i_ressources_ménage égal à 67 250 €
  définition f.i_date_courante égal à |2020-05-01|
  définition f.i_résidence égal à Métropole
  règle f.i_personne_charge_effective_permanente_est_parent rempli
  assertion f.i_montant_versé = 99,46€

déclaration champ d'application Test5:
  f champ d'application InterfaceAllocationsFamiliales
  données champ d'application Données

champ d'application Test5:
  définition f.i_enfants égal à [données.enfant1]
  définition f.i_ressources_ménage égal à 30 000 €
  définition f.i_date_courante égal à |2020-05-01|
  définition f.i_résidence égal à Métropole
  règle f.i_personne_charge_effective_permanente_est_parent rempli
  assertion f.i_montant_versé = 0,00€

déclaration champ d'application Test6:
  f champ d'application InterfaceAllocationsFamiliales

champ d'application Test6:
  définition f.i_enfants égal à [EnfantEntrée {
    -- d_identifiant: 0
    -- d_date_de_naissance: |2009-11-10|
    -- d_rémuneration_mensuelle: 439€
    -- d_prise_en_charge: EffectiveEtPermanente
    -- d_a_déjà_ouvert_droit_aux_allocations_familiales: vrai
    -- d_bénéficie_titre_personnel_aide_personnelle_logement: faux
  };EnfantEntrée {
    -- d_identifiant: 1
    -- d_date_de_naissance: |2020-09-25|
    -- d_rémuneration_mensuelle: 1949€
    -- d_prise_en_charge: EffectiveEtPermanente
    -- d_a_déjà_ouvert_droit_aux_allocations_familiales: vrai
    -- d_bénéficie_titre_personnel_aide_personnelle_logement: faux
  }]
  définition f.i_ressources_ménage égal à 78 830 €
  définition f.i_date_courante égal à |2020-05-01|
  définition f.i_résidence égal à Guadeloupe
  règle f.i_personne_charge_effective_permanente_est_parent rempli
  assertion f.i_montant_versé = 66,30€

déclaration champ d'application Test7:
  f champ d'application InterfaceAllocationsFamiliales
  données champ d'application Données

champ d'application Test7:
  définition f.i_enfants égal à
    [données.enfant1;données.enfant2;données.enfant3;données.enfant4]
  définition f.i_ressources_ménage égal à 30 000 €
  définition f.i_date_courante égal à |2021-02-01|
  définition f.i_résidence égal à Métropole
  règle f.i_personne_charge_effective_permanente_est_parent rempli
  assertion f.i_montant_versé = 417,84€

déclaration champ d'application Test8:
  f champ d'application InterfaceAllocationsFamiliales

champ d'application Test8:
  définition f.i_enfants égal à [EnfantEntrée {
    -- d_identifiant: 0
    -- d_date_de_naissance: |2004-01-01|
    -- d_rémuneration_mensuelle: 0€
    -- d_prise_en_charge: EffectiveEtPermanente
    -- d_a_déjà_ouvert_droit_aux_allocations_familiales: vrai
    -- d_bénéficie_titre_personnel_aide_personnelle_logement: faux
  };EnfantEntrée {
    -- d_identifiant: 1
    -- d_date_de_naissance: |2005-01-01|
    -- d_rémuneration_mensuelle: 0€
    -- d_prise_en_charge: EffectiveEtPermanente
    -- d_a_déjà_ouvert_droit_aux_allocations_familiales: vrai
    -- d_bénéficie_titre_personnel_aide_personnelle_logement: faux
  }]
  définition f.i_ressources_ménage égal à 69945 €
  définition f.i_date_courante égal à |2021-01-01|
  définition f.i_résidence égal à Métropole
  règle f.i_personne_charge_effective_permanente_est_parent rempli
  assertion f.i_montant_versé = 197,90 €

déclaration champ d'application Test9:
  f champ d'application InterfaceAllocationsFamiliales

champ d'application Test9:
  définition f.i_enfants égal à [EnfantEntrée {
    -- d_identifiant: 0
    -- d_date_de_naissance: |2001-07-27|
    -- d_rémuneration_mensuelle: 1258€
    -- d_prise_en_charge: ServicesSociauxAllocationVerséeAuxServicesSociaux
    -- d_a_déjà_ouvert_droit_aux_allocations_familiales: vrai
    -- d_bénéficie_titre_personnel_aide_personnelle_logement: faux
  };EnfantEntrée {
    -- d_identifiant: 1
    -- d_date_de_naissance: |2008-04-27|
    -- d_rémuneration_mensuelle: 1766€
    -- d_prise_en_charge: ServicesSociauxAllocationVerséeAuxServicesSociaux
    -- d_a_déjà_ouvert_droit_aux_allocations_familiales: vrai
    -- d_bénéficie_titre_personnel_aide_personnelle_logement: faux
  }]
  définition f.i_ressources_ménage égal à 75786 €
  définition f.i_date_courante égal à |2020-05-01|
  définition f.i_résidence égal à Guadeloupe
  règle f.i_personne_charge_effective_permanente_est_parent rempli
  assertion f.i_montant_versé = 0€

déclaration champ d'application Test10:
  f champ d'application InterfaceAllocationsFamiliales

champ d'application Test10:
  définition f.i_enfants égal à [EnfantEntrée {
    -- d_identifiant: 0
    -- d_date_de_naissance: |2003-02-22|
    -- d_rémuneration_mensuelle: 0€
    -- d_prise_en_charge: EffectiveEtPermanente
    -- d_a_déjà_ouvert_droit_aux_allocations_familiales: vrai
    -- d_bénéficie_titre_personnel_aide_personnelle_logement: faux
  };EnfantEntrée {
    -- d_identifiant: 1
    -- d_date_de_naissance: |2013-09-30|
    -- d_rémuneration_mensuelle: 300€
    -- d_prise_en_charge: GardeAlternéePartageAllocations
    -- d_a_déjà_ouvert_droit_aux_allocations_familiales: vrai
    -- d_bénéficie_titre_personnel_aide_personnelle_logement: faux
  }]
  définition f.i_ressources_ménage égal à 30000 €
  définition f.i_date_courante égal à |2020-04-20|
  définition f.i_résidence égal à Métropole
  règle f.i_personne_charge_effective_permanente_est_parent rempli
  assertion f.i_montant_versé = 99,46€

déclaration champ d'application Test11:
  f champ d'application InterfaceAllocationsFamiliales

champ d'application Test11:
  définition f.i_enfants égal à [EnfantEntrée {
    -- d_identifiant: 0
    -- d_date_de_naissance: |2003-02-22|
    -- d_rémuneration_mensuelle: 0€
    -- d_prise_en_charge: EffectiveEtPermanente
    -- d_a_déjà_ouvert_droit_aux_allocations_familiales: vrai
    -- d_bénéficie_titre_personnel_aide_personnelle_logement: faux
  };EnfantEntrée {
    -- d_identifiant: 1
    -- d_date_de_naissance: |2013-09-30|
    -- d_rémuneration_mensuelle: 300€
    -- d_prise_en_charge: EffectiveEtPermanente
    -- d_a_déjà_ouvert_droit_aux_allocations_familiales: vrai
    -- d_bénéficie_titre_personnel_aide_personnelle_logement: faux
  };
  EnfantEntrée {
    -- d_identifiant: 2
    -- d_date_de_naissance: |2007-04-16|
    -- d_rémuneration_mensuelle: 0€
    -- d_prise_en_charge: EffectiveEtPermanente
    -- d_a_déjà_ouvert_droit_aux_allocations_familiales: vrai
    -- d_bénéficie_titre_personnel_aide_personnelle_logement: faux
  }]
  définition f.i_ressources_ménage égal à 30000 €
  définition f.i_date_courante égal à |2021-12-31|
  définition f.i_résidence égal à Métropole
  règle f.i_personne_charge_effective_permanente_est_parent rempli
  assertion f.i_montant_versé = 435,55€

déclaration champ d'application Test12:
  f champ d'application InterfaceAllocationsFamiliales

champ d'application Test12:
  définition f.i_enfants égal à [EnfantEntrée {
    -- d_identifiant: 0
    -- d_date_de_naissance: |2003-02-22|
    -- d_rémuneration_mensuelle: 0€
    -- d_prise_en_charge: EffectiveEtPermanente
    -- d_a_déjà_ouvert_droit_aux_allocations_familiales: vrai
    -- d_bénéficie_titre_personnel_aide_personnelle_logement: faux
  };EnfantEntrée {
    -- d_identifiant: 1
    -- d_date_de_naissance: |2013-09-30|
    -- d_rémuneration_mensuelle: 300€
    -- d_prise_en_charge: EffectiveEtPermanente
    -- d_a_déjà_ouvert_droit_aux_allocations_familiales: vrai
    -- d_bénéficie_titre_personnel_aide_personnelle_logement: faux
  };
  EnfantEntrée {
    -- d_identifiant: 2
    -- d_date_de_naissance: |2007-04-16|
    -- d_rémuneration_mensuelle: 0€
    -- d_prise_en_charge: EffectiveEtPermanente
    -- d_a_déjà_ouvert_droit_aux_allocations_familiales: vrai
    -- d_bénéficie_titre_personnel_aide_personnelle_logement: faux
  };
  EnfantEntrée {
    -- d_identifiant: 3
    -- d_date_de_naissance: |2012-02-02|
    -- d_rémuneration_mensuelle: 0€
    -- d_prise_en_charge: EffectiveEtPermanente
    -- d_a_déjà_ouvert_droit_aux_allocations_familiales: vrai
    -- d_bénéficie_titre_personnel_aide_personnelle_logement: faux
  }]
  définition f.i_ressources_ménage égal à 20000 €
  définition f.i_date_courante égal à |2019-08-26|
  définition f.i_résidence égal à Mayotte
  règle f.i_avait_enfant_à_charge_avant_1er_janvier_2012 rempli
  règle f.i_personne_charge_effective_permanente_est_parent rempli
  assertion f.i_montant_versé = 315,84€

déclaration champ d'application Test13:
  f champ d'application InterfaceAllocationsFamiliales

champ d'application Test13:
  définition f.i_enfants égal à [EnfantEntrée {
    -- d_identifiant: 0
    -- d_date_de_naissance: |2008-02-22|
    -- d_rémuneration_mensuelle: 0€
    -- d_prise_en_charge: EffectiveEtPermanente
    -- d_a_déjà_ouvert_droit_aux_allocations_familiales: vrai
    -- d_bénéficie_titre_personnel_aide_personnelle_logement: faux
  }]
  définition f.i_ressources_ménage égal à 65000 €
  définition f.i_date_courante égal à |2019-08-26|
  définition f.i_résidence égal à Mayotte
  règle f.i_personne_charge_effective_permanente_est_parent rempli
  assertion f.i_montant_versé = 34,99€
```
